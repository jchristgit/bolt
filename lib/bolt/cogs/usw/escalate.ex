defmodule Bolt.Cogs.USW.Escalate do
  @moduledoc false

  @behaviour Nosedrum.TextCommand

  alias Bolt.Repo
  alias Bolt.Schema.USWPunishmentConfig
  alias Nosedrum.TextCommand.Predicates
  alias Nostrum.Api

  @impl true
  def usage, do: ["usw escalate [on|off]"]

  @impl true
  def description,
    do: """
    Toggles automatic punishment escalation.
    Use the command without any argument to show whether automatic punishment is currently enabled.
    Enable it using `on`, disable it again using `off`.

    Requires the `MANAGE_GUILD` permission.
    """

  @impl true
  def predicates,
    do: [&Predicates.guild_only/1, Predicates.has_permission(:manage_guild)]

  @impl true
  def command(msg, []) do
    response =
      case Repo.get(USWPunishmentConfig, msg.guild_id) do
        nil ->
          "ğŸš« USW punishment is not configured"

        %USWPunishmentConfig{escalate: escalate} ->
          "â„¹ automatic punishment escalation is " <> if escalate, do: "enabled", else: "disabled"
      end

    {:ok, _msg} = Api.create_message(msg.channel_id, response)
  end

  def command(msg, ["on"]) do
    response =
      case Repo.get(USWPunishmentConfig, msg.guild_id) do
        nil ->
          "ğŸš« USW punishment is not configured"

        config ->
          changeset = USWPunishmentConfig.changeset(config, %{escalate: true})
          {:ok, _updated_config} = Repo.update(changeset)
          "ğŸ‘Œ automatic punishment escalation is now enabled"
      end

    {:ok, _msg} = Api.create_message(msg.channel_id, response)
  end

  def command(msg, ["off"]) do
    response =
      case Repo.get(USWPunishmentConfig, msg.guild_id) do
        nil ->
          "ğŸš« USW punishment is not configured"

        config ->
          changeset = USWPunishmentConfig.changeset(config, %{escalate: false})
          {:ok, _updated_config} = Repo.update(changeset)
          "ğŸ‘Œ automatic punishment escalation is now disabled"
      end

    {:ok, _msg} = Api.create_message(msg.channel_id, response)
  end

  def command(msg, _args) do
    response = "â„¹ï¸ usage: `usw escalate [on|off]`"
    {:ok, _msg} = Api.create_message(msg.channeL_id, response)
  end
end
