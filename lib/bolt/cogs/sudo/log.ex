defmodule Bolt.Cogs.Sudo.Log do
  @moduledoc false

  alias Bolt.ModLog
  alias Bolt.Repo
  alias Bolt.Schema.ModLogConfig
  alias Nostrum.Api
  import Ecto.Query, only: [from: 2]

  @spec command(Nostrum.Struct.Message.t(), [String.t()]) :: {:ok, Nostrum.Struct.Message.t()}
  def command(msg, content_list) when content_list != [] do
    content = Enum.join(content_list, " ")
    startup_note = "â² distributing event, please wait"
    {:ok, startup_msg} = Api.create_message(msg.channel_id, startup_note)

    query =
      from(
        conf in ModLogConfig,
        where: conf.event == "BOT_UPDATE",
        distinct: conf.guild_id,
        select: conf.guild_id
      )

    query
    |> Repo.all()
    |> Enum.each(fn guild_id ->
      ModLog.emit(
        guild_id,
        "BOT_UPDATE",
        content
      )

      :timer.sleep(200)
    end)

    response = "ğŸ‘Œ event broadcasted to subscribed guilds"
    {:ok, _msg} = Api.create_message(msg.channel_id, response)
    Api.delete_message(startup_msg)
  end

  def command(msg, []) do
    response = "ğŸš« content to log must not be empty"
    {:ok, _msg} = Api.create_message(msg.channel_id, response)
  end
end
